plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.5'
    id 'io.spring.dependency-management' version '1.1.4'
}

allprojects {
    group = 'com.travel'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '21'

    repositories {
        mavenCentral()
    }
}

// bootJar 작업은 실행 가능한 jar을 생성하려고 시도하기 때문에 이를 위해서는 main() 메서드가 필요합니다.
// Root 프로젝트는 main 없이 라이브러리의 역할을 하는 모듈이기 때문에 false로 비활성화해줍니다.
bootJar.enabled = false

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'io.micrometer:micrometer-registry-prometheus'
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

project(':travel-batch-core') {
    dependencies {
        implementation project(':travel-yaml-importer')
        implementation project(':travel-system-core')
        api project(':travel-domain-rds')
    }
    bootJar { enabled = true }
    jar { enabled = true }
}

project(':travel-domain-rds') {
    dependencies {
        compileOnly project(':travel-yaml-importer')
        api project(':travel-system-core')
    }
    bootJar { enabled = false }
    jar { enabled = true }
}

project(':travel-system-core') {
    bootJar { enabled = false }
    jar { enabled = true }
}

project(':travel-yaml-importer') {
    bootJar { enabled = false }
    jar { enabled = true }
}

tasks.named('wrapper') {
    gradleVersion="8.7"
}